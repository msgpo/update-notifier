# Maintainer: Vlad Zagorodniy <vladzzag@gmail.com>
pkgname=update-notifier
pkgver=1.1.r0.de695bf
pkgrel=1
pkgdesc="PackageKit update notifier"
arch=('x86_64')
url="https://github.com/zzag/update-notifier"
license=('MIT')
depends=(glibc packagekit)
makedepends=(git go)
source=("git+https://github.com/zzag/$pkgname.git")
sha256sums=('SKIP')

pkgver() {
    cd "$pkgname"

    printf "%s" "$(git describe --long --tags | sed 's/\([^-]*-\)g/r\1/;s/-/./g')"
}

prepare() {
    cd "$pkgname/hooks"

    # go ignores vendor directory if the project is built outside of GOPATH
    mkdir src && cd src
    ln -s ../vendor/github.com
    ln -s ../vendor/golang.org
}

build() {
    cd "$pkgname/hooks"

    GOPATH=`pwd` go build -o refresh-packagekit-cache
}

package() {
    cd "$pkgname"

    install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

    install -Dm755 "hooks/refresh-packagekit-cache" "$pkgdir/usr/lib/update-notifier/refresh-packagekit-cache"
    install -Dm644 "hooks/refresh-packagekit-cache.hook" "$pkgdir/usr/share/libalpm/hooks/refresh-packagekit-cache.hook"

    install -Dm644 "systemd/update-notifier.service" "$pkgdir/usr/lib/systemd/system/update-notifier.service"
    install -Dm644 "systemd/update-notifier.timer" "$pkgdir/usr/lib/systemd/system/update-notifier.timer"
}

install=$pkgname.install
